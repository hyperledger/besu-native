/*
 * Copyright Hyperledger Besu contributors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License"); you may not use this file except in compliance with
 * the License. You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software distributed under the License is distributed on
 * an "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the License for the
 * specific language governing permissions and limitations under the License.
 *
 * SPDX-License-Identifier: Apache-2.0
 */

plugins {
    id 'java-library'
    id 'maven-publish'
    id 'com.jfrog.artifactory' version '5.2.3'
}

dependencies {
    implementation project(':common')
    implementation 'net.java.dev.jna:jna:5.12.1'
    testImplementation 'com.google.guava:guava:31.1-jre'
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'io.consensys.tuweni:tuweni-bytes:2.7.1'
    testImplementation 'io.consensys.tuweni:tuweni-units:2.7.1'
    testImplementation 'org.assertj:assertj-core:3.22.0'
}

// Ensure that the compileTestJava task depends on the macArmLibCopy task
tasks.named('compileTestJava').configure {
    dependsOn tasks.named('macArmLibCopy')
    dependsOn tasks.named('macLibCopy')
    dependsOn tasks.named('linuxLibCopy')
    dependsOn tasks.named('linuxArm64LibCopy')
}

task macArmLibCopy(type: Copy) {
    from 'besu-native-ec/release/darwin-aarch64/libbesu_native_ec.dylib'
    from 'besu-native-ec/release/darwin-aarch64/libbesu_native_ec_crypto.dylib'
    into 'build/resources/main/lib-native/aarch64'
}
processResources.dependsOn macArmLibCopy

task macLibCopy(type: Copy) {
    from 'besu-native-ec/release/darwin-x86-64/libbesu_native_ec.dylib'
    from 'besu-native-ec/release/darwin-x86-64/libbesu_native_ec_crypto.dylib'
    into 'build/resources/main/lib-native/x86-64'
}
processResources.dependsOn macLibCopy

task linuxLibCopy(type: Copy) {
    from 'besu-native-ec/release/linux-gnu-x86_64/libbesu_native_ec.so'
    from 'besu-native-ec/release/linux-gnu-x86_64/libbesu_native_ec_crypto.so'
    into 'build/resources/main/lib-native/x86-64'
}
processResources.dependsOn linuxLibCopy

task linuxArm64LibCopy(type: Copy) {
    from 'besu-native-ec/release/linux-gnu-aarch64/libbesu_native_ec.so'
    from 'besu-native-ec/release/linux-gnu-aarch64/libbesu_native_ec_crypto.so'
    into 'build/resources/main/lib-native/aarch64'
}
processResources.dependsOn linuxArm64LibCopy

task linuxRiscv64LibCopy(type: Copy) {
    from 'besu-native-ec/release/linux-gnu-riscv64/libbesu_native_ec.so'
    from 'besu-native-ec/release/linux-gnu-riscv64/libbesu_native_ec_crypto.so'
    into 'build/resources/main/lib-native/riscv64'
}
processResources.dependsOn linuxRiscv64LibCopy

jar {
    archiveBaseName = 'besu-native-secp256r1'
    includeEmptyDirs = false
    manifest {
        attributes(
                'Specification-Title': archiveBaseName,
                'Specification-Version': project.version,
                'Implementation-Title': archiveBaseName,
                'Implementation-Version': project.version,
                'Automatic-Module-Name': 'org.hyperledger.besu.nativelib.secp256r1'
        )
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveBaseName = 'besu-native-secp256r1'
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveBaseName = 'besu-native-secp256r1'
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId "org.hyperledger.besu"
            artifactId 'secp256r1'
            version "${project.version}"

            from components.java
            artifact sourcesJar
            artifact javadocJar

            pom {
                name = "Besu Native - ${project.name}"
                description = 'Adapter for native secp256r1 library'
                url = 'http://github.com/hyperledger/besu-native'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/hyperledger/besu-native.git'
                    developerConnection = 'scm:git:ssh://github.com/hyperledger/besu-native.git'
                    url = 'https://github.com/hyperledger/besu-native'
                }
            }
        }
    }
}

def artifactoryUser = project.hasProperty('artifactoryUser') ? project.property('artifactoryUser') : System.getenv('ARTIFACTORY_USER')
def artifactoryKey = project.hasProperty('artifactoryApiKey') ? project.property('artifactoryApiKey') : System.getenv('ARTIFACTORY_KEY')
def artifactoryRepo = System.getenv('ARTIFACTORY_REPO') ?: 'besu-maven'
def artifactoryOrg = System.getenv('ARTIFACTORY_ORG') ?: 'hyperledger'

artifactory {
    contextUrl = "https://hyperledger.jfrog.io/${artifactoryOrg}"
    publish {
        repository {
            repoKey = artifactoryRepo
            username = artifactoryUser
            password = artifactoryKey
        }
        defaults {
            publications('mavenJava')
            publishArtifacts = true
            publishPom = true
        }
    }
}

test {
    systemProperty "jna.library.path", "$project.projectDir/besu-native-ec/release"
}
