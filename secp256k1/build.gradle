/*
 * Copyright ConsenSys AG.
 *
 * Licensed under the Apache License, Version 2.0 (the "License"); you may not use this file except in compliance with
 * the License. You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software distributed under the License is distributed on
 * an "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the License for the
 * specific language governing permissions and limitations under the License.
 *
 * SPDX-License-Identifier: Apache-2.0
 */

plugins {
  id 'java-library'
  id 'maven-publish'
  id 'com.jfrog.bintray' version '1.8.4'
}

dependencies {
  implementation 'net.java.dev.jna:jna:5.5.0'
}

task macLibCopy(type: Copy) {
  from 'build/lib/libsecp256k1.dylib'
  into 'build/resources/main/darwin'
  rename(/(\w+)\.dylib/, '$1.jnilib')
}
jar.dependsOn macLibCopy

task linuxLibCopy(type: Copy) {
  from 'build/lib/libsecp256k1.so'
  into 'build/resources/main/linux-x86-64'
}
jar.dependsOn linuxLibCopy

jar {
  archiveBaseName = 'besu-native-secp256k1'
  manifest {
    attributes(
        'Specification-Title': archiveBaseName,
        'Specification-Version': project.version,
        'Implementation-Title': archiveBaseName,
        'Implementation-Version': project.version,
    )
  }
}

task sourcesJar(type: Jar, dependsOn: classes) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from javadoc.destinationDir
}


apply plugin: 'com.jfrog.bintray'

def bintrayUser = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
def bintrayKey = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_KEY')

// These are made able to be overridden so that we can test publishing to a test repo
def bintrayRepo = System.getenv('BINTRAY_REPO') != "" ? System.getenv('BINTRAY_REPO') : 'besu-repo'
def bintrayOrg = System.getenv('BINTRAY_ORG') != "" ? System.getenv('BINTRAY_ORG') : 'hyperledger-org'

def bintrayPackage = bintray.pkg {
  repo = bintrayRepo
  name = 'besu-native-secp256k1'
  userOrg = bintrayOrg
  licenses = ['Apache-2.0']
  websiteUrl = 'https://github.com/hyperledger/besu-native'
  vcsUrl = 'https://github.com/hyperledger/besu-native.git'

  version {
    name = project.version
    released = new Date()
  }
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      groupId "org.hyperledger.besu"
      version "${project.version}"

      from components.java
      artifact sourcesJar
      artifact javadocJar

      pom {
        name = "Besu Native - ${project.name}"
        description = 'Adapter for native secp256k1 library'
        url = 'http://github.com/hyperledger/besu-native'
        licenses {
          license {
            name = 'The Apache License, Version 2.0'
            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
          }
        }
        scm {
          connection = 'scm:git:git://github.com/hyperledger/besu-native.git'
          developerConnection = 'scm:git:ssh://github.com/hyperledger/besu-native.git'
          url = 'https://github.com/hyperledger/besu-native'
        }
      }
    }
  }
}

bintray {
  user = bintrayUser
  key = bintrayKey

  publications = ['mavenJava']
  override = version.endsWith('SNAPSHOT')

  publish = true

  pkg = bintrayPackage
}

