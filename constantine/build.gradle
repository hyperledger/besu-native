plugins {
    id 'java-library'
    id 'maven-publish'
    id 'com.jfrog.artifactory' version '4.20.0'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'net.java.dev.jna:jna:5.12.1'
    testImplementation 'net.java.dev.jna:jna:5.12.1'
    implementation 'org.apache.tuweni:tuweni-bytes:2.2.0'
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.assertj:assertj-core:3.22.0'
    testImplementation 'org.mockito:mockito-core:4.4.0'
}

task compileNim(type: Exec) {
    commandLine '/Users/nicks/.nimble/bin/nim', 'c', '--noMain', '-d:release', '--app:lib', '--out:build/libs/libconstantine_eip196.so', './constantine_eip196_bindings.nim'
}

task copyNimLib(type: Copy, dependsOn: compileNim) {
    from 'build/libs/libconstantine_eip196.so'
    into 'src/main/resources/native'
}

processResources.dependsOn copyNimLib

jar {
    archiveBaseName = 'besu-native-constantine'
    includeEmptyDirs = false
    manifest {
        attributes(
                'Specification-Title': archiveBaseName,
                'Specification-Version': project.version,
                'Implementation-Title': archiveBaseName,
                'Implementation-Version': project.version,
                'Automatic-Module-Name': 'org.hyperledger.besu.nativelib.constantine'
        )
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveBaseName = 'besu-native-constantine'
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveBaseName = 'besu-native-constantine'
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId "org.hyperledger.besu"
            artifactId "constantine"
            version "${project.version}"

            from components.java
            artifact sourcesJar
            artifact javadocJar

            pom {
                name = "Besu Native - ${project.name}"
                description = 'Constantine'
                url = 'http://github.com/hyperledger/besu-native'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/hyperledger/besu-native.git'
                    developerConnection = 'scm:git:ssh://github.com/hyperledger/besu-native.git'
                    url = 'https://github.com/hyperledger/besu-native'
                }
            }
        }
    }
}

def artifactoryUser = project.hasProperty('artifactoryUser') ? project.property('artifactoryUser') : System.getenv('ARTIFACTORY_USER')
def artifactoryKey = project.hasProperty('artifactoryApiKey') ? project.property('artifactoryApiKey') : System.getenv('ARTIFACTORY_KEY')
def artifactoryRepo = System.getenv('ARTIFACTORY_REPO') ?: 'besu-maven'
def artifactoryOrg = System.getenv('ARTIFACTORY_ORG') ?: 'hyperledger'

artifactory {
    contextUrl = "https://hyperledger.jfrog.io/${artifactoryOrg}"
    publish {
        repository {
            repoKey = artifactoryRepo
            username = artifactoryUser
            password = artifactoryKey
        }
        defaults {
            publications('mavenJava')
            publishArtifacts = true
            publishPom = true
        }
    }
}
